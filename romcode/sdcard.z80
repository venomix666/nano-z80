; Basic SD-card routines for the nano-Z80

; SD boot
; Read boot sector and boot from sd card
; Currently uses an offset 0x01000000 block to allow
; using the same SD-card for nano6502 and nanoZ80
sd_boot:
        call sd_init
        ; Read the boot sector
        ld de, $0100
        ld bc, $0000
        call sd_set_sector

        ; Do a manual read instead of reading to RAM
        ; Read strobe
        out (SD_READ),a
        call sd_wait

        ; Ensure that we are looking at the beginning of the sector
        ld a,0
        out (SD_PAGE),a

        ; Check if valid boot sector
        in a,(SD_DATA)
        cp a,$6e
        ret nz
        in a,(SD_DATA+1)
        cp a,$61
        ret nz
        in a,(SD_DATA+2)
        cp a,$6e
        ret nz
        in a,(SD_DATA+3)
        cp a,$6f
        ret nz

        ; Read start sector address
        in a,(SD_DATA+4)
        ld (SD_ADDR_BUF),a
        ;ld d,a
        in a,(SD_DATA+5)
        ld (SD_ADDR_BUF+1),a
        ;ld e,a
        in a,(SD_DATA+6)
        ld (SD_ADDR_BUF+2),a
        ;ld b,a
        in a,(SD_DATA+7)
        ld (SD_ADDR_BUF+3),a
        ;ld c,a

        ; Read number of pages
        in a,(SD_DATA+8)
        ld (PAGE_CNT), a

        ; Read store address
        in a,(SD_DATA+9)
        ld h,a
        ld (BOOT_PAGE), a
        ld l,0

        

sd_boot_loop:
        ld a, (SD_ADDR_BUF)
        ld d, a
        ld a, (SD_ADDR_BUF+1)
        ld e, a
        ld a, (SD_ADDR_BUF+2)
        ld b, a
        ld a, (SD_ADDR_BUF+3)
        ld c, a
        call sd_set_sector
        call sd_read_sector
        
        ld a,(PAGE_CNT)
        dec a
        ld (PAGE_CNT),a
        cp a,0
        ret z
        ld a, (SD_ADDR_BUF+2)
        ld b, a
        ld a, (SD_ADDR_BUF+3)
        ld c, a
        inc bc
        ld a, b
        ld (SD_ADDR_BUF+2), a
        ld a, c
        ld (SD_ADDR_BUF+3), a
        ;ld a, c
        or b
        jr nz,sd_boot_loop
        ld a, (SD_ADDR_BUF)
        ld d, a
        ld a, (SD_ADDR_BUF+1)
        ld e, a

        inc de
        ld a, d
        ld (SD_ADDR_BUF), a
        ld a, e
        ld (SD_ADDR_BUF+1),a
        jr sd_boot_loop
        
; SD read sector
; Reads full 512 byte sector to the memory address in hl
sd_read_sector:
        ; Wait until the SD card is ready
        call sd_init

        ; Read strobe
        out (SD_READ),a
       
        ; Wait for read to finsih 
        call sd_wait

        ; Copy data to RAM
        ld a,0
        ld d,0
sd_read_sector_copy:
        out (SD_PAGE),a
        ld c, SD_DATA
sd_read_loop:
        in a,(c)
        ld (hl),a
        inc hl
        inc c
        ld a,c
        cp a,0
        jr nz, sd_read_loop
        inc d
        ld a,d
        cp a,4
        jr nz, sd_read_sector_copy

        ret

; SD set sector
; Sets the SD card sector
; Expects the sector address in de (msw) and bc (lsw)
sd_set_sector:       
        call sd_init

        ; Set sector
        ld a,c
        out (SD_ADDR_0),a
        ld a,b
        out (SD_ADDR_1),a
        ld a,e
        out (SD_ADDR_2),a
        ld a,d
        out (SD_ADDR_3),a
        
        ret

sd_init:
       ; Set the SD IO bank 
        ld a, IO_SELECT_SD
        out (IO_BANK),a

        ; Fall through
        ; Wait unit SD card is ready
sd_wait:
       in a,(SD_BUSY)
       cp a,0
       jr nz, sd_wait
       ret 
